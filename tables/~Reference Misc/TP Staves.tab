# ------------------------------------------------------
# Last Update: modified by Valminder Jan 11, 2014.
# ------------------------------------------------------
#
# Note for customizing: Each table assumes that
# the items are listed in order of value.
#
# Modified by Valminder, January 2014.
#    Added link to: 
#         Creatures.All
#         Miscellaneous.Metal
#         Miscellaneous.Shape
#         Miscellaneous.Stone
#         Miscellaneous.Wood
#         Rune Generator.GenRune
#

%minVal%,0
%maxVal%,99999999
%power%,0
%format%,0
%tries%,0
%maxTries%,0
%temp%,0
%inRange%,0
%group%,0
%item%,0
%name%,0
%value%,0
%desc%,0
%aura%,0
%valMinItem%,0
%valMaxItem%,0
%itemsDS%,0
%index%,0
%weight%,0

%charges%,0
%rndCharges%,0
%other%,0
%clue%,0

@minVal,0,Enter minimum value (in gp):
@maxVal,99999999,Enter maximum value (in gp):
@power,4,Degree of Power:,Minor,Medium,Major,Random
@rndCharges,1,Random number of charges?,No,Yes
@format,1,Select format:,Human-readable,Delimited for other table
@maxTries,10,Number of attempts:

:Start
1,{DSRead~itemsDS,TP Staves}
_|temp=[CheckParams]|
_{If~%temp%=OK?[Continue]/%temp%}
_{Status~}

;RandomCharges
2,{Dice~1d4}
6,{Dice~1d12+4}
9,{Dice~1d18+16}
6,{Dice~1d12+34}
2,{Dice~1d4+46}

:Continue
1,|tries=0|{While~%tries%<%maxTries%,[GenItem][CheckValueInRange]{If~%inRange%=yes?|tries=maxTries|/|tries+1|}}
_{If~%inRange%=no?[Error=4]/ [DisplayItem=%format%]}

:GenItem
1,{Status~Attempt STAFF #{Calc~%tries%+1} of %maxTries%}
_|group={Iif~%power%=4?{Param~power,{Dice~1d3}}:{Param~power,%power%}}|
_|index={DSRoll~itemsDS,{LCase~%group%}roll}|
_|name=staff of {DSGet~itemsDS,%index%,name}|
_|value={DSGet~itemsDS,%index%,value}|
_|charges={If~%rndCharges%=2?[RandomCharges]/50}|
_|value*%charges%|
_|aura={DSGet~itemsDS,%index%,aura}|
_|weight={DSGet~itemsDS,%index%,weight}|
_|desc=[GenDesc]|

:DisplayItem
1,{Bold~{Cap~%name%}}
_{CR~}Description: {Cap~%desc%}{CR~}Charges: %charges%{CR~}Value: %value% gp
_{CR~}Aura: {Italic~%aura%}
2,%name% (%charges% charges)!%value%!%weight%!%aura%!%desc%

# ----------------------------------------------------
# Initialization and parameter validation
:CheckParams
1,{If~%minVal%<=%maxVal%?OK/ [Error=1]}

:CheckValueInRange
1,|inRange={If~{And~%minVal%<=%value%,%value%<=%maxVal%}=1?yes/no}|

:Error
1,ERR001: Invalid min and/or max values
2,ERR002: Minimum value given greater than maximum value for selected group
3,ERR003: Maximum value given less than minimum value for selected group
4,ERR005: Couldn't generate item in %tries% tries
# ----------------------------------------------------

:GenDesc
1,staff [Size], [Made][Special][Carvings][Clue]

:Size
1,{Dice~1d4+3}' long and {Dice~1d3}" thick

;Made
1,[MatlDesc] [Material]

;MatlDesc
3,crafted from
3,made of
3,shaped from
1,grown out of
1,carved from

;Material
9,wood {Reset~Miscellaneous.Wood}([~Miscellaneous.Wood])
1,[OtherMatl]

;OtherMatl
12,bone ([~Creatures.All])
8,[Metal]

;Metal
1,[~Miscellaneous.Metal]
3,brass
3,iron
1,steel
1,glass

;Special
6,|other=0|
2,; a [WandGem] is fixed to one end|other=1|
1,; a strange device is affixed to one end|other=1|
1,; it is shod in [Metal-1] at {If~{Dice~1d2}=1?one end|other=1|/both ends|other=2|}

;WandGem
1,emerald
1,diamond
1,ruby
1,sapphire
1,[~Miscellaneous.Stone]

;Carvings
3,|clue={AorAn~[ClueType]} provides|
3,, and it is decorated with carvings {Reset~Rune Generator.Rune2}{Reset~Rune Generator.Weapon}([NPC Appearance.Tatoo])|clue= providing|
2,, and it is covered with runes[ClueGlyph]|clue=, the runes providing|

;Clue
3,%clue% some clue to the staff's function
7,

;ClueType
1,glyph[ClueGlyph] [ClueMethod]
1,rune[ClueGlyph] [ClueMethod]
1,symbol[ClueGlyph] [ClueMethod]

;ClueGlyph
1, {Reset~Rune Generator.Rune2}{Reset~Rune Generator.Weapon}([~Rune Generator.GenRune])
1, ([~Miscellaneous.Shape])

;ClueMethod
1,carved into [ClueLocation]
1,scratched into [ClueLocation]
1,painted on [ClueLocation]
1,engraved in [ClueLocation]

;ClueLocation
1,the side
1,{If~%other%!=1?one end/the other end}
