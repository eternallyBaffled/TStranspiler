# ---------------------------------------------------
# Last Update: modified by Valminder August 24, 2006.
# ---------------------------------------------------
#
# Magical Weapons
#
# Sean Hlousek
#
# -------------------------------------------------------------------------
#
# To call this table from another one:
#    [EQ Magic Weapons.Generate(%Rolls%,%GenItem%)
#
#    Parameters: (%Rolls%,%GenItem%)
#
#    Rolls:    (1) Enter the number of items needed.
#    Gen Item: (1) 1= Weapons (Magical) Includes magical ranged weapons
#                  2= Ranged Weapons and Ammo (Magical)
#
# -------------------------------------------------------------------------
#
# Modified by Valminder, August  2006.
#    Adapted for TSv5.
#    I changed the "START" line.
#    I heavily modified this table. I could modify it more by
#       removing all the variables for the cost, but it would
#       be too long. For now, it works fine.
#    The final price need to be adjusted for some items, but it
#       seems relatively OK.
#    I diminished the price variation in [Store].
#    Cost of Items: Put the cost in GP, the output will modify it
#       for gp, sp, or cp.
#       1 sp = 0.1 gp
#       1 cp = 0.01 gp
#


### Was %Base%
%PercentGPMod%,1
### This is the Store modifier on all its prices 70% to 200% (normal 95% - 105%)
%STVR%,0
### How many items to roll
%Rolls%,0
#### Was %Base1%
%MagicalGPMod%,0

# New variables Aug 2006.
%Item%,0
%Book%,0
%Cost%,0
%Condition%,0
%GenItem%,0
# 1= Mundane Item, 2= Magical Item
%Magic%,1
%MWGPMod%,0
%WeaponSizeMod%,1
# To be used with "EQ_Main.tab"
%GetData%,0
%DataName%,0
%DataPercentMod%,0
%MoneyType%,gp

@Rolls,10,How many Items are in this Shop?
@GenItem,1,What do you want to generate,Weapons (Magical) Includes Magical Ranged Weapons,Ranged Weapons and Ammo (Magical)apons and Ammo (Mundane and Magical) No melee weapons

:Start
1,{Status~By Sean Hlousek, Valminder 2006}[Generate]

:Generate
1,|STVR=1|[Store]
#_[Multiplier]
_{Loop~%Rolls%,|PercentGPMod=0||MagicalGPMod=0||Magic=2||MWGPMod=0||WeaponSizeMod=1|
_[ItemGen=%GenItem%]}

:Print
1,<b>Item:</b> <i><font color="Purple">%Item%</font></i>[Print2]
2,<b>Item:</b> <i><font color="Blue">(Magic) %Item%</font></i>[Print2]

:Print2
1, <font size=1>(%Book%)</font> ([ItemSize] size)
_|Cost+%MagicalGPMod%||Cost+%MWGPMod%|{If~%Magic%=1?|Cost*%WeaponSizeMod%|}
_[Calculate Cost]
_ (%Condition%, [AddComma.Start(%Cost%)] %MoneyType%)<br>

# This will give a modifier to the cost of the items.
;Store
2,|STVR-{Calc~({Dice~1d10+20}/100)}|
4,|STVR-{Calc~({Dice~1d10+10}/100)}|
8,|STVR-{Calc~({Dice~1d10+5}/100)}|
15,|STVR-{Calc~({Dice~1d5}/100)}|
70,|STVR=1|
30,|STVR+{Calc~({Dice~1d5}/100)}|
15,|STVR+{Calc~({Dice~1d10+5}/100)}|
8,|STVR+{Calc~({Dice~1d10+10}/100)}|
4,|STVR+{Calc~({Dice~1d15+20}/100)}|
2,|STVR+{Calc~({Dice~1d25+35}/100)}|
1,|STVR+{Calc~({Dice~1d50+50}/100)}|

:Multiplier
1,Price Multiplier: %STVR%<br>

:Calculate Cost
1,|Cost={Calc~((%STVR%+%PercentGPMod%)*%Cost%)}|
_{If~%Cost%>=5?|Cost={Trunc~%Cost%}||MoneyType=gp|}
_{If~{And~%Cost%>=1,%Cost%<5}=1?{If~%Cost%={Trunc~%Cost%}?|MoneyType=gp|/|Cost={Round~1,%Cost%}||Cost*10||MoneyType=sp|}}
_{If~%Cost%<0.1?|Cost*100||Cost={Trunc~%Cost%}|{If~%Cost%=0?|Cost=1|}|MoneyType=cp|/{If~%Cost%<1?|Cost*10||Cost={Trunc~%Cost%}|{If~%Cost%=0?|Cost=1|}|MoneyType=sp|}}

:ItemGen
1,[EQ Blacksmith.Generate(1,3)]
2,[EQ Blacksmith.Generate(1,15)]
