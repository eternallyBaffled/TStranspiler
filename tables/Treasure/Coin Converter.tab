# ------------------------------------------------------
# Last Update: verified by Valminder Dec 01, 2013.
#
# Sometimes it crashes but I cannot seem to find the error.
#
# ------------------------------------------------------
#
# Coin Converter
#
# Converts Copper Pieces to PP, GP, SP, and CP and puts them in containers
# Originally an addon for Anthony Marker's 3E_Treasure Generator www.Breminor.com
#     Created by Vance Reed for TableSmith 3.0 (Upgraded to 5.0)
#     Updated 1-23-2012 Error corrections
#                       Added easy way to modify table to custom coinage of your realm
#
# -------------------------------------------------------------------------
#
# To call this table from another one:
#    [Coin Converter.Generate(%InputValue%,%World%)
#
#     or (without World information)
#
#    [Coin Converter.GenerateALT(%InputValue%,%World%) 
#
#    Parameters: (%InputValue%,%World%)
#
#    InputValue:   Value of treasure in gold pieces.
#    World:        1= Basic D&D (Greyhawk)
#                  2= Basic D&D (with Mithril Pieces 1=100gp)
#                  3= Dragonlance
#                  4= Eberron
#
# -------------------------------------------------------------------------
# 
# Modified by Valminder, December 2013.
#    I changed the "START" line.
#    Modified "Reset" to take into account the "size" variables below.
#    Added "Parameters" to change the World you play in.
#         -Basic D&D (Greyhawk), Dragonlance, Eberron, Forgotten Realms, Dark Sun
#         -Added a few more variables to add more coins.
#    Modified Containers as per 
#         -"The Most Complete Equipement Guide in the Known Crystal Spheres 3.5E"
#         -http://forum.rpg.net/showthread.php?560895-How-many-coins-fit-in-a-chest/page2 
#    Added links to:
#         AddComma.Start
#
#
#
##### To change this table to reflect the coinage of your game, change the names, quantities and
##### values for CoinA - Coin6 in "Set World" subtable below
##### Coin3 is the base coin (usually the gold piece)
##### ALWAYS put a value for Coin2 and Coin3, but they can be the only 2 coins used.

/OverRideRolls 1

##### You can change the quantity that a container can hold in "SetWorld" subtable below #####
# Valminder: I modified the content of the chest to match the data found on this web site : 
# http://forum.rpg.net/showthread.php?560895-How-many-coins-fit-in-a-chest/page2 
# The amount of coins which can fit in a 1'x1'x1' container is 12,342 ... I round it to 12,000
#      Large Chest (50 pounds empty) - 3'x2'x2' holds up to 144000 coins
%LargeChestSize%,144000
%LargeChestWeight%,50
#      Medium Chest (40 pounds empty) - 3'x2'x1' holds up to 72000 coins
%MediumChestSize%,72000
%MediumChestWeight%,40
#      Small Chest (25 pounds empty) - 2'x1'x1' holds up to 24000 coins
%SmallChestSize%,24000
%SmallChestWeight%,25
#       Large Sack (.5 pounds empty)  - 2'x1'x1' holds up to 1500 coins (30lbs before bursting, but you could fill it with 24000 coins)
%LargeSackSize%,1500
%LargeSackWeight%,.5
#        Small Sack (0 pounds empty)  - 1'x1'x8" holds up to 750 coins (15 lbs before bursting, but you could fill it with 8000 coins)
%SmallSackSize%,750
%SmallSackWeight%,0
#        Backpack Leather (2 pounds empty)  - 1'x1'x1' holds up to 2500 coins (50 lbs before bursting, but you could fill it with 24000 coins)
%BackpackMedSize%,2500
%BackpackMedWeight%,2
#        Pouch (0 pounds empty)       - holds up to 150 coins
%PouchSize%,150
%PouchWeight%,0
#        Barrel 30 Gal (12 pounds empty)       - 4 cu.ft. - holds up to 48000 coins
%Barrel30Size%,48000
%Barrel30Weight%,12

##### You can change the names of the coins of the realm in "SetWorld" subtable below #####
##### Set the piece name to "XXX" if you do not want this piece, for example if your world has only
##### 4 types of coins set CoinName5 to "XXX"
%CoinNameA%,XXX
%CoinName0%,XXX
%CoinName1%,Copper Piece
%CoinName2%,Silver Piece
%CoinName3%,Gold Piece
%CoinName4%,Platinum Piece
%CoinName5%,Mithril Piece
%CoinName6%,XXX

##### You can change the weight of each type of coin in "SetWorld" subtable below ####
#####                Number of coins per pound            #####
%CoinWeightA%,50
%CoinWeight0%,50
%CoinWeight1%,50
%CoinWeight2%,50
%CoinWeight3%,50
%CoinWeight4%,50
%CoinWeight5%,50
%CoinWeight6%,50

##### You can change the value of each type of coin in "SetWorld" subtable below #####
#####    Values are in comparison to 1 piece of Coin3    #####
%CoinValueA%,10000
%CoinValue0%,1000
%CoinValue1%,100
%CoinValue2%,10
%CoinValue3%,1
%CoinValue4%,.1
%CoinValue5%,.01
%CoinValue6%,.001

%CoinA%,0
%Coin0%,0
%Coin1%,1
%Coin2%,0
%Coin3%,0
%Coin4%,0
%Coin5%,0
%Coin6%,0
%r%,0
%x%,0
%y%,0
%z%,0
%InputValue%,0
%coins%,0
%Barrel30%,0
%ChestL%,0
%ChestM%,0
%ChestS%,0
%PackM%,0
%SackL%,0
%SackS%,0
%Pouch%,0
%Roll%,0
%w%,0
%w1%,0
%w2%,0
%w3%,0
%w4%,0
%w5%,0
%World%,0

@InputValue,10000,Enter the number of Gold Pieces (or base coin) to calculate coins for
@World,1,Choose your World,Basic D&D (Greyhawk),Basic (with Mithril Pieces 1=100gp),Dragonlance,Eberron,Forgotten Realms,Dark Sun
# Program forwards # of Gold Piece (Coin3) value to this table to distribute it among mp (Coin5), pp (Coin4), gp (Coin3), sp (Coin2) and cp (Coin1).
# Changing the values in the "SetWorld subtable will change the max number of coins in each type of container.

:Start
1,{Status~Vance Reed 2012, Valminder 2013}[Generate].

:Generate
1,[Reset]
_<b>Total Coins:</b> Value of [AddComma.Start(%InputValue%)] {Plural~%CoinName3%}.<br><br>
_{If~%CoinName6%!=XXX? [Coin6 Check] {If~%Coin6%>0? %CoinName6%{If~%Coin6%>1?s}<br>}}
_{If~%CoinName5%!=XXX? [Coin5 Check] {If~%Coin5%>0? %CoinName5%{If~%Coin5%>1?s}<br>}}
_{If~%CoinName4%!=XXX? [Coin4 Check] {If~%Coin4%>0? %CoinName4%{If~%Coin4%>1?s}<br>}}
_{If~%CoinName3%!=XXX? [Coin3 Check] {If~%Coin3%>0? %CoinName3%{If~%Coin3%>1?s}<br>}}
_{If~%CoinName2%!=XXX? [Coin2 Check] {If~%Coin2%>0? %CoinName2%{If~%Coin2%>1?s}<br>}}
_{If~%CoinName1%!=XXX? {If~%CoinName0%=XXX? {If~%CoinNameA%=XXX? [Generate1]/[Generate1b]}/[Generate1b]}}
_{If~%CoinName0%!=XXX? {If~%CoinNameA%=XXX? [Generate0]/[Generate0b]}}
_{If~%CoinNameA%!=XXX? [GenerateA]}
_<br><br><b><u>World Information</u>:</b> [WorldInfo=%World%]

:GenerateALT
1,[Reset]
_<b>Total Coins:</b> Value of [AddComma.Start(%InputValue%)] {Plural~%CoinName3%}.<br>
_{If~%CoinName6%!=XXX? [Coin6 Check] {If~%Coin6%>0? %CoinName6%{If~%Coin6%>1?s}<br>}}
_{If~%CoinName5%!=XXX? [Coin5 Check] {If~%Coin5%>0? %CoinName5%{If~%Coin5%>1?s}<br>}}
_{If~%CoinName4%!=XXX? [Coin4 Check] {If~%Coin4%>0? %CoinName4%{If~%Coin4%>1?s}<br>}}
_{If~%CoinName3%!=XXX? [Coin3 Check] {If~%Coin3%>0? %CoinName3%{If~%Coin3%>1?s}<br>}}
_{If~%CoinName2%!=XXX? [Coin2 Check] {If~%Coin2%>0? %CoinName2%{If~%Coin2%>1?s}<br>}}
_{If~%CoinName1%!=XXX? {If~%CoinName0%=XXX? {If~%CoinNameA%=XXX? [Generate1]/[Generate1b]}/[Generate1b]}}
_{If~%CoinName0%!=XXX? {If~%CoinNameA%=XXX? [Generate0]/[Generate0b]}}
_{If~%CoinNameA%!=XXX? [GenerateA]}

:Generate1
1,|InputValue*%CoinValue1%||x={Round~0,%x%}||Coin1=%InputValue%|[AddComma.Start(%Coin1%)] %CoinName1%{If~%Coin1%>1?s}</b><br>
_|coins+%Coin1%|([AddComma.Start(%coins%)] total coins weighing 
_|x={Calc~{Calc~%CoinA%/ %CoinWeightA%}+{Calc~%Coin0%/ %CoinWeight0%}+{Calc~%Coin1%/ %CoinWeight1%}+{Calc~%Coin2%/ %CoinWeight2%}+{Calc~%Coin3%/ %CoinWeight3%}+{Calc~%Coin4%/ %CoinWeight4%}+{Calc~%Coin5%/ %CoinWeight5%}+{Calc~%Coin6%/ %CoinWeight6%}}|
_|x={Round~0,%x%}|[AddComma.Start(%x%)] pounds)<br><br>
_<b>Location/Containers:</b><br>
_|x={Trunc~%x%}|
_[Containers+%x%]

:Generate1b
1,[Coin1 Check] {If~%Coin1%>0? %CoinName1%{If~%Coin1%>1?s}<br>}

:Generate0
1,|InputValue*%CoinValue0%||x={Round~0,%x%}||Coin0=%InputValue%|[AddComma.Start(%Coin0%)] %CoinName0%{If~%Coin0%>1?s}</b><br>
_|coins+%Coin0%|([AddComma.Start(%coins%)] total coins weighing 
_|x={Calc~{Calc~%CoinA%/ %CoinWeightA%}+{Calc~%Coin0%/ %CoinWeight0%}+{Calc~%Coin1%/ %CoinWeight1%}+{Calc~%Coin2%/ %CoinWeight2%}+{Calc~%Coin3%/ %CoinWeight3%}+{Calc~%Coin4%/ %CoinWeight4%}+{Calc~%Coin5%/ %CoinWeight5%}+{Calc~%Coin6%/ %CoinWeight6%}}|
_|x={Round~0,%x%}|[AddComma.Start(%x%)] pounds)<br><br>
_<b>Location/Containers:</b><br>
_|x={Trunc~%x%}|
_[Containers+%x%]

:Generate0b
1,[Coin0 Check] {If~%Coin0%>0? %CoinName0%{If~%Coin0%>1?s}<br>}

:GenerateA
1,|InputValue*%CoinValueA%||x={Round~0,%x%}||CoinA=%InputValue%||CoinA={Round~0,%CoinA%}|[AddComma.Start(%CoinA%)] %CoinNameA%{If~%CoinA%>1?s}</b><br>
_|coins+%CoinA%|([AddComma.Start(%coins%)] total coins weighing 
_|x={Calc~{Calc~%CoinA%/ %CoinWeightA%}+{Calc~%Coin0%/ %CoinWeight0%}+{Calc~%Coin1%/ %CoinWeight1%}+{Calc~%Coin2%/ %CoinWeight2%}+{Calc~%Coin3%/ %CoinWeight3%}+{Calc~%Coin4%/ %CoinWeight4%}+{Calc~%Coin5%/ %CoinWeight5%}+{Calc~%Coin6%/ %CoinWeight6%}}|
_|x={Round~0,%x%}|[AddComma.Start(%x%)] pounds)<br><br>
_<b>Location/Containers:</b><br>
_|x={Trunc~%x%}|
_[Containers+%x%]

#|CoinA={Round~0,%CoinA%}|

######################################## Coin Type Distribution ##############################    WORKS FINE
:Coin6 Check
1-9,
10,|x=%InputValue%||x*%CoinValue6%||x={Round~0,%x%}|{If~%x%>1?[Coin6=1]/[Coin6=2]}
:Coin6
1,|y=%x%||z=%x%||z\10||x\2||Coin6={Dice~1d%x%+%z%}|{If~%Coin6%>%y%?[Coin6=3]}|x=%Coin6%||x/ %CoinValue6%||InputValue-%x%||coins+%Coin6%| [AddComma.Start(%Coin6%)]
2,
3,|Coin6=%y%|

:Coin5 Check
1-8,
# Annotated out 9-10 so no Coin5 show up.      
9-10,|x=%InputValue%||x*%CoinValue5%||x={Round~0,%x%}|{If~%x%>1?[Coin5=1]/[Coin5=2]}
:Coin5
1,|y=%x%||z=%x%||z\10||x\2||Coin5={Dice~1d%x%+%z%}|{If~%Coin5%>%y%?[Coin5=3]}|x=%Coin5%||x/ %CoinValue5%||InputValue-%x%||coins+%Coin5%| [AddComma.Start(%Coin5%)]
2,
3,|Coin5=%y%|

:Coin4 Check
1-5,
6-10,|x=%InputValue%||x*%CoinValue4%||x={Round~0,%x%}|{If~%x%>1?[Coin4=1]/[Coin4=2]}
:Coin4
1,|y=%x%||z=%x%||z\10||x\4||Coin4={Dice~1d%x%+%z%}|{If~%Coin4%>%y%?[Coin4=3]}|x=%Coin4%||x/ %CoinValue4%||InputValue-%x%||coins+%Coin4%| [AddComma.Start(%Coin4%)]
2,
3,|Coin4=%y%|

:Coin3 Check
1-2,
3-15,|x=%InputValue%||x*%CoinValue3%||x={Round~0,%x%}|{If~%x%>1?[Coin3=1]/[Coin3=2]}
:Coin3
1,|y=%x%||z=%x%||z\10||x\4||Coin3={Dice~4d%x%+%z%}|{If~%Coin3%>%y%?[Coin3=3]}|x=%Coin3%||InputValue-%x%||coins+%Coin3%| [AddComma.Start(%Coin3%)]
2,
3,|Coin3=%y%|

:Coin2 Check
1,
2-25,|x=%InputValue%||x*%CoinValue2%||x={Round~0,%x%}|{If~%x%>1?[Coin2=1]/[Coin2=2]}
:Coin2
1,|y=%x%||z=%x%||z\10||x\6||Coin2={Dice~6d%x%+%z%}|{If~%Coin2%>%y%?[Coin2=3]}|x=%Coin2%||x/ %CoinValue2%||InputValue-%x%||coins+%Coin2%| [AddComma.Start(%Coin2%)]
2,
3,|Coin2=%y%|

:Coin1 Check
1,
2-30,|x=%InputValue%||x*%CoinValue1%||x={Round~0,%x%}|{If~%x%>1?[Coin1=1]/[Coin1=2]}
:Coin1
1,|y=%x%||z=%x%||z\10||x\8||Coin1={Dice~8d%x%+%z%}|{If~%Coin1%>%y%?[Coin1=3]}|x=%Coin1%||x/ %CoinValue1%||InputValue-%x%||coins+%Coin1%| [AddComma.Start(%Coin1%)]
2,
3,|Coin1=%y%|

:Coin0 Check
1,
2-35,|x=%InputValue%||x*%CoinValue0%||x={Round~0,%x%}|{If~%x%>1?[Coin0=1]/[Coin0=2]}
:Coin0
1,|y=%x%||z=%x%||z\10||x\10||Coin0={Dice~10d%x%+%z%}|{If~%Coin0%>%y%?[Coin0=3]}|x=%Coin0%||x/ %CoinValue0%||InputValue-%x%||coins+%Coin0%| [AddComma.Start(%Coin0%)]
2,
3,|Coin0=%y%|

#------------------------------------------------------------------------------
#                                     Containers for coins 
#------------------------------------------------------------------------------
;Containers
#----------------------------     Determines how many coins in each container.....returns variable %x% coins --------------
#   Since this roll is modified by the amount of coins, adjusting the value of choice 1 will change how often a "pile" of coins comes up.
500,[Loose Coins=1]<br>
#----------------------------     This allows for a chance of just a loose pile of coins
10,{Loop~3,[Containers1]}
_{Loop~4,[Containers2]}
_[Loose Coins=1]

;!Containers1
1,[Large Chest]
1,[Medium Chest]
1,[Small Chest]

;!Containers2
1,[Large Sack]
1,[Small Sack]
1,[Pouch]
1,[MiscContainer]

;MiscContainer
1,[Backpack Medium]
1,[Barrel 30Gal]

#------------------------------------------------------------------------------------------------------------------
#                      Large Chest - holds up to 144000 coins
#                      MediumChest - holds up to 72000 coins
#                      Small Chest - holds up to 24000 coins
#                      Barrel 30Gal- holds up to 48000 coins
#                      Backpack Med- holds up to 2500 coins
#                      Large Sack  - holds up to 1500 coins
#                      Small Sack  - holds up to 750 coins
#                      Pouch       - holds up to 150 coins
# The size of the containers can be modified in "SetWorld" subtable below.
#------------------------------------------------------------------------------------------------------------------

:Large Chest
1,
2-10,|y=%coins%||z=%y%|{If~%y%>20000?[LC=1]/[LC=2]}
### 20000 minimum coins in a large chest
:LC
1,
_{If~%y%>%LargeChestSize%?[LC=3]}|y-20000|
_|y\3||x={Dice~3d%y%+20000}|{If~%x%>%ChestL%?[LC=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A large chest</b>  containing |w=0|[Coin Amounts=1] (weighing |w+%LargeChestWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds)<br>[Large Chest]
2,
3,|y=%LargeChestSize%|
4,|x=%ChestL%|
5,|x=%z%|

:Medium Chest
1,
2-10,|y=%coins%||z=%y%|{If~%y%>10000?[MC=1]/[MC=2]}
### 10000 minimum coins in a medium chest
:MC
1,
_{If~%y%>%MediumChestSize%?[MC=3]}|y-10000|
_|y\3||x={Dice~3d%y%+10000}|{If~%x%>%ChestM%?[MC=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A medium chest</b>  containing |w=0|[Coin Amounts=1] (weighing |w+%MediumChestWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds)<br>[Medium Chest]
2,
3,|y=%MediumChestSize%|
4,|x=%ChestM%|
5,|x=%z%|

:Small Chest
1,
2-9,|y=%coins%||z=%y%|{If~%y%>4000?[SC=1]/[SC=2]}
### 4000 minimum coins in a small chest
:SC
1,{If~%y%>%SmallChestSize%?[SC=3]}|y-4000|
_|y\3||x={Dice~3d%y%+4000}|{If~%x%>%ChestS%?[SC=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A small chest</b> containing |w=0|[Coin Amounts=1] (weighing |w+%SmallChestWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds)<br>[Small Chest] 
2,
3,|y=%SmallChestSize%|
4,|x=%ChestS%|
5,|x=%z%|

:Backpack Medium
1,
2-8,|y=%coins%||z=%y%|{If~%y%>500?[BPM=1]/[BPM=2]}
### 500 minimum coins in a backpack
:BPM
1,{If~%y%>%BackpackMedSize%?[BPM=3]}|y-500|
_|y\3||x={Dice~3d%y%+500}|{If~%x%>%PackM%?[BPM=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A Medium-sized leather backpack</b> containing |w=0|[Coin Amounts=1] (weighing |w+%BackpackMedWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds) <br>[Backpack Medium]
1,{If~%y%>%BackpackMedSize%?[BPM=3]}|y-500|[BPM1]
2,
3,|y=%BackpackMedSize%|
4,|x=%PackM%|
5,|x=%z%|

:Barrel 30Gal
1,
2-10,|y=%coins%||z=%y%|{If~%y%>8000?[B30=1]/[B30=2]}
### 8000 minimum coins in a 30Gal Barrel chest
:B30
1,
_{If~%y%>%Barrel30Size%?[B30=3]}|y-8000|
_|y\3||x={Dice~3d%y%+10000}|{If~%x%>%Barrel30%?[B30=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A 30 gal. barrel</b>  containing |w=0|[Coin Amounts=1] (weighing |w+%Barrel30Weight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds)<br>[Barrel 30Gal]
2,
3,|y=%MediumChestSize%|
4,|x=%ChestM%|
5,|x=%z%|

:Large Sack
1,
2-8,|y=%coins%||z=%y%|{If~%y%>500?[LS=1]/[LS=2]}
### 500 minimum coins in a large sack
:LS
1,{If~%y%>%LargeSackSize%?[LS=3]}|y-500|
_|y\3||x={Dice~3d%y%+500}|{If~%x%>%SackL%?[LS=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A large sack</b> containing |w=0|[Coin Amounts=1] (weighing |w+%LargeSackWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds) <br>[Large Sack]
2,
3,|y=%LargeSackSize%|
4,|x=%SackL%|
5,|x=%z%|

:Small Sack
1,
2-6,|y=%coins%||z=%y%|{If~%y%>150?[SS=1]/[SS=2]}
### 150 minimum coins in a small sack
:SS
1,{If~%y%>%SmallSackSize%?[SS=3]}|y-150|
_|y\3||x={Dice~3d%y%+150}|{If~%x%>%SackS%?[SS=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A small sack</b> containing |w=0|[Coin Amounts=1] (weighing |w+%SmallSackWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pounds)<br>[Small Sack]
2,
3,|y=%SmallSackSize%|
4,|x=%SackS%|
5,|x=%z%|

:Pouch
1,
2-4,|y=%coins%||z=%y%|{If~%y%>25?[P=1]/[P=2]}
### 25 minimum coins in a small sack
:P
1,{If~%y%>%PouchSize%?[P=3]}|y-25|
_|x={Dice~1d%y%+25}|{If~%x%>%Pouch%?[P=4]}{If~%x%>%z%?[SS=5]}|coins-%x%|
_<b>A pouch</b> containing |w=0|[Coin Amounts=1] (weighing |w+%PouchWeight%||w={Round~0,%w%}|[AddComma.Start(%w%)] pound{If~%w%<2?/s})<br>[Pouch]
2,
3,|y=%PouchSize%|
4,|x=%Pouch%|
5,|x=%z%|

:Loose Coins
1,|y=%coins%|{If~%y%>0?[Loose Coins=2]}
2,<b>Laying loose around the area</b>|w=0|
_{If~%Coin6%>0?[Loose Coins=3]}
_{If~%Coin5%>0?[Loose Coins=4]}
_{If~%Coin4%>0?[Loose Coins=5]}
_{If~%Coin3%>0?[Loose Coins=6]}
_{If~%Coin2%>0?[Loose Coins=7]}
_{If~%Coin1%>0?[Loose Coins=8]}
_{If~%Coin0%>0?[Loose Coins=9]}
_{If~%CoinA%>0?[Loose Coins=10]}
_  (weighing |w\50|[AddComma.Start(%w%)] pounds)
3, [AddComma.Start(%Coin6%)] %CoinName6%{If~%Coin6%>1?s} |w+%Coin6%|,
4, [AddComma.Start(%Coin5%)] %CoinName5%{If~%Coin5%>1?s} |w+%Coin5%|,
5, [AddComma.Start(%Coin4%)] %CoinName4%{If~%Coin4%>1?s} |w+%Coin4%|,
6, [AddComma.Start(%Coin3%)] %CoinName3%{If~%Coin3%>1?s}|w+%Coin3%|,
7, [AddComma.Start(%Coin2%)] %CoinName2%{If~%Coin2%>1?s}|w+%Coin2%|,
8, [AddComma.Start(%Coin1%)] %CoinName1%{If~%Coin1%>1?s}|w+%Coin1%|
9, [AddComma.Start(%Coin0%)] %CoinName0%{If~%Coin0%>1?s}|w+%Coin0%|
10, [AddComma.Start(%CoinA%)] %CoinNameA%{If~%CoinA%>1?s}|w+%CoinA%|

#--------------------------------------------------------------------------------------
#                                         Coin Split for each container 
#                                        Variable %x% is number of coins
#--------------------------------------------------------------------------------------

:Coin Amounts
#<-------------------------------  Checks to see if there is any of a type of coin to split, if no, go to next type  
1,{If~%x%>0?{If~%Coin6%>0?[Coin Amounts=2]}}
_{If~%x%>0?{If~%Coin5%>0?[Coin Amounts=3]}}
_{If~%x%>0?{If~%Coin4%>0?[Coin Amounts=4]}}
_{If~%x%>0?{If~%Coin3%>0?[Coin Amounts=5]}}
_{If~%x%>0?{If~%Coin2%>0?[Coin Amounts=6]}}
_{If~%x%>0?{If~%Coin1%>0?[Coin Amounts=7]}}
_{If~%x%>0?{If~%Coin0%>0?[Coin Amounts=8]}}
_{If~%x%>0?{If~%CoinA%>0?[Coin Amounts=17]}}
#<-------------------------------    How many??   
####### Max random roll is 20000 ... keeps TS from crashing
####### If random roll of type > number left for this container, make it max.
2, |Roll=%Coin6%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10}
####### Check to see if the remaining coin types have enough coins to fill the order, if not, roll again
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%Coin2%||z+%Coin3%||z+%Coin4%||z+%Coin5%||z+%r%|{If~%z%<%x%?[Coin Amounts=2]/[Coin Amounts=11]}
3, |Roll=%Coin5%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%Coin2%||z+%Coin3%||z+%Coin4%||z+%r%|{If~%z%<%x%?[Coin Amounts=3]/[Coin Amounts=12]}
4, |Roll=%Coin4%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%Coin2%||z+%Coin3%||z+%r%|{If~%z%<%x%?[Coin Amounts=4]/[Coin Amounts=13]}
5, |Roll=%Coin3%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%Coin2%||z+%r%|{If~%z%<%x%?[Coin Amounts=5]/[Coin Amounts=14]}
6, |Roll=%Coin2%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%r%|{If~%z%<%x%?[Coin Amounts=6]/[Coin Amounts=15]}
7,{If~%CoinName0%=XXX? {If~%CoinNameA%=XXX? [Coin_Amounts_1b]/[Coin_Amounts_1a]}/[Coin_Amounts_1a]}
8,{If~%CoinNameA%=XXX? [Coin_Amounts_0b]/[Coin_Amounts_0a]}
9,{If~%CoinNameA%!=XXX? [Coin_Amounts_A]}
###################################################################
10,|r=%x%|
#<-------------------------------    Print  
11,|Coin6-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight6%}| [AddComma.Start(%r%)] %CoinName6%{If~%r%>1?s},
12,|Coin5-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight5%}| [AddComma.Start(%r%)] %CoinName5%{If~%r%>1?s},
13,|Coin4-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight4%}| [AddComma.Start(%r%)] %CoinName4%{If~%r%>1?s}, 
14,|Coin3-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight3%}| [AddComma.Start(%r%)] %CoinName3%{If~%r%>1?s}, 
15,|Coin2-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight2%}| [AddComma.Start(%r%)] %CoinName2%{If~%r%>1?s}, 
16,|Coin1-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight1%}| [AddComma.Start(%r%)] %CoinName1%{If~%r%>1?s} 
17,|Coin0-%r%||x-%r%||w+{Calc~%r%/ %CoinWeight0%}| [AddComma.Start(%r%)] %CoinName0%{If~%r%>1?s} 
17,|CoinA-%r%||w+{Calc~%r%/ %CoinWeightA%}| [AddComma.Start(%r%)] %CoinNameA%{If~%r%>1?s} 

:Coin_Amounts_1a
1, |Roll=%Coin1%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%Coin1%||z+%r%|{If~%z%<%x%?[Coin Amounts=7]/[Coin Amounts=16]}

:Coin_Amounts_1b
1,|r=%Coin1%|{If~%r%>%x%?[Coin Amounts=10]}|z=%r%|[Coin Amounts=16]

:Coin_Amounts_0a
1, |Roll=%Coin0%|{If~%Roll%>20000?[Roll Max=1]}|r={Dice~1d%Roll%}|{If~%r%>%x%?[Coin Amounts=10]}
_|z=%CoinA%||z+%Coin0%||z+%r%|{If~%z%<%x%?[Coin Amounts=8]/[Coin Amounts=17]}

:Coin_Amounts_0b
1,|r=%Coin0%|{If~%r%>%x%?[Coin Amounts=10]}|z=%r%|[Coin Amounts=18]

:Coin_Amounts_A
1,|r=%CoinA%|{If~%r%>%x%?[Coin Amounts=9]}|z=%r%|[Coin Amounts=18]

:Roll Max
1,|Roll=20000|
2,
3,

:Reset
1,|ChestL=%LargeChestSize%||ChestM=%MediumChestSize%||ChestS=%SmallChestSize%||PackM=%BackpackMedSize%|
_|Barrel30=%Barrel30Size%|
_|SackL=%LargeSackSize%||SackS=%SmallSackSize%||Pouch=%PouchSize%||Roll=0|
_[SetWorld=%World%]
#Changing the values in the :Reset group will change the max number of coins in each type of container.

:SetWorld
# Coin Weight = number of coins per pound.
# Coin Value = in comparison to 1 piece of Coin3 (usually Gold)
# Weight = Weight of empty container.
# Basic D&D (Greyhawk)
1,|CoinNameA=XXX||CoinName0=XXX||CoinName1=Copper Piece||CoinName2=Silver Piece||CoinName3=Gold Piece||CoinName4=Platinum Piece||CoinName5=XXX||CoinName6=XXX|
_|CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50|
_|CoinValue1=100||CoinValue2=10||CoinValue3=1||CoinValue4=.1|
# Basic D&D (with Mithril pieces 1=100 gp)
2,|CoinNameA=XXX||CoinName0=XXX||CoinName1=Copper Piece||CoinName2=Silver Piece||CoinName3=Gold Piece||CoinName4=Platinum Piece||CoinName5=Mithril Piece||CoinName6=XXX|
_|CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50||CoinWeight5=50|
_|CoinValue1=100||CoinValue2=10||CoinValue3=1||CoinValue4=.1||CoinValue5=.01|
# Dragonlance
3,|CoinNameA=Copper Piece||CoinName0=Gold Piece||CoinName1=Silver Piece||CoinName2=Iron or Bronze Piece||CoinName3=Steel Piece||CoinName4=Platinum Piece||CoinName5=XXX||CoinName6=XXX|
_|CoinWeightA=50||CoinWeight0=50||CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50|
_|CoinValueA=100||CoinValue0=40||CoinValue1=20||CoinValue2=2||CoinValue3=1||CoinValue4=.2|
# Eberron
4,|CoinNameA=Crown (Copper) Piece||CoinName0=Double Crown Piece||CoinName1=Sovereign (Silver) Piece||CoinName2=Silver Throne Piece||CoinName3=Galifar (Gold) Piece||CoinName4=Platinum Dragon Piece||CoinName5=Astral Diamond||CoinName6=XXX|
_|CoinWeightA=50||CoinWeight0=50||CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50||CoinWeight5=50|
_|CoinValueA=100||CoinValue0=50||CoinValue1=10||CoinValue2=.5||CoinValue3=1||CoinValue4=.1||CoinValue5=.0001|
# Forgotten Realms
5,|CoinNameA=XXX||CoinName0=XXX||CoinName1=Copper Piece||CoinName2=Silver Piece||CoinName3=Gold Piece||CoinName4=Platinum Piece||CoinName5=One-pound Silver Trade Bar||CoinName6=One-pound Gold Trade Bar|
_|CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50||CoinWeight5=1||CoinWeight6=1|
_|CoinValue1=100||CoinValue2=10||CoinValue3=1||CoinValue4=.1||CoinValue5=.2||CoinValue6=.02|
# Dark Sun
6,|CoinNameA=XXX||CoinName0=XXX||CoinName1=Bit||CoinName2=Bit||CoinName3=Ceramic Piece||CoinName4=Silver Piece||CoinName5=Electrum Piece||CoinName6=Gold Piece|
_|CoinWeight1=50||CoinWeight2=50||CoinWeight3=50||CoinWeight4=50||CoinWeight5=50||CoinWeight6=50|
_|CoinValue1=10||CoinValue2=10||CoinValue3=1||CoinValue4=.1||CoinValue5=.02||CoinValue6=.01|

:WorldInfo
# Basic D&D (Greyhawk)
1,Basic D&D (Greyhawk)<br>100 cp = 10 sp = 1 gp = 1/10 pp
# Basic D&D (with Mithril pieces 1=100 gp)
2,Basic D&D with Mithril pieces<br>100 cp = 10 sp = 1 gp = 1/10 pp = 1/100 mp
# Dragonlance
3,Dragonlance<br>1 Steel Piece = 1 gp in regular D&D Equipment Guides.<br>100 cp = 40 gp = 20 sp = 2 Iron/Bronze = 1 Steel<br>1 pp = 5 gp
# Eberron
4,Eberron<br>100 cp = 10 sp = 1 gp = 1/10 pp<br>1 Astral Diamond = 10,000 Gold Galifars<br>1 Double Crown = 2 Copper Crowns<br>1 Silver Throne = 5 Silver Sovereign
# Forgotten Realms
5,Forgotten Realms<br>100 cp = 10 sp = 1 gp = 1/10 pp<br>One-pound Silver Bar = 5 gp<br>One-pound Gold bar = 50 gp
# Dark Sun
6,Dark Sun<br>1 Ceramic piece = 1 gp in regular D&D Equipment Guides for non-metal items.<br>Metallic items are still worth the same price in gold pieces.
_<br>Magical Items retain their gold pieces price.<br>100 cp = 10 sp = 2 ep = 1 gp = 1/5 pp