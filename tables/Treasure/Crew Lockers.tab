# ------------------------------------------------------
# Last Update: verified by Valminder Aug 27, 2014.
# ------------------------------------------------------
#
# -----------------------------------------------------------
# Last Update; Larry Cormier October 23, 2012
# -----------------------------------------------------------
#
# Skull & Shackles Crew Lockers Contents
#
# by Larry Cormier Adapted for TableSmith from Paizo’s “Skulls &
# Shackles Adventure Path; The Wormwood Mutiny
#
# CREW LOCKERS
# Each pirate on the crew is assigned a locker with a poor lock (Disable Device DC 15) to hold their
# personal effects (though some characters have provided their own better locks). Each pirate’s locker
# contains coinage (mostly silver) worth 1d20 gp (unless that character is currently gambling) and 1d3
# objects from the following list. Pirates refer to this cache as their duffle.
#
# Modified by Valminder, August 2014.
#    Added link to:
#       Equipment.Alchemical
#       Equipment.Armors Only
#       Equipment.MiscItems
#       Equipment.Poisons
#       Equipment.Shields Only
#       Equipment.Weapons
#       Miscellaneous.Metal
# 

# Define Variables
%Choice%,6
%gp%,0
%Chance%,0
%ItemCount%,0
%Cost%,0
%Book%,0
%Meaning%,0

#Coin Hoard Variables
%HowMany%,0
%ManyFactor%,0
%Worth%,0
%ValueFactor%,0
%Gems%,0
%TempNumber%,0
%Total%,0
%Count%,0
%Chance%,0
%Quantity%,0
%NumberKind%,0
%Kind%,0

:Start
1,{Status~by Larry Cormier derived from Paizo’s Skull and Shackles Adventure Path; the Wormwood Mutiny}
_{Line~Align,Width}
_<b><u>Crew Footlocker Contents</u>:</b><br>
_|Quantity=2|
_A [ConstructionMaterials] footlocker protected with {AorAn~[LockQuality]} that currently contains:<br>
_[Coin Hoard.GenCoins]<br><br>
_<b><i>This Footlocker also contains:</b></i>
_{Loop~{Dice~1d3},<br>-[Generate].}
_[Chanceof]{If~%Chance%<10?{Loop~{Dice~1d3},|ItemCount=1|
_<br>- {AorAn~[Furnishings and Appointments.ClothingAndFootwear]}.}}
_[Chanceof]{If~%Chance%<5?{Loop~{Dice~1d2},|ItemCount=1|
_<br>- {AorAn~[Furnishings and Appointments.TypicallyBejeweledItems] worth [CalculatePrice={Dice~1d2}]%Cost% gp}.}}

:Chanceof
1,|Chance={Dice~1d100}|

:Generate
1,[AdditionalItems]

:AdditionalGold
1,|gp+{Dice~2d10}|
2,|gp+{Dice~1d10}|

:ConstructionMaterials
1,Wooden ([~Miscellaneous.Wood])
2,Metal ([~Miscellaneous.Metal])

;!AdditionalItems
2, [Gems2.Generate(%ShowClass%,%ShowDesc%)]
2, {AorAn~[Chanceof]{If~%Chance%<10?Masterwork/} [Musical Instruments]} worth %Cost% gp
11, an alchemical item ([~Equipment.Alchemical])
2, a Flask of holy water
2, a bedroll
2, a Silver ring worth 5 gp
2, a Gold earring worth 15 gp
2, a bullseye lantern
2, a set of [Chanceof]{If~%Chance%<10?Masterwork/Common} [ToolsType] tools
1, a lucky rabbit’s foot
1, a Bicorne hat
1, a Tricorne hat
1, a store of |ItemCount={Dice~1d6}| {If~%ItemCount%>1? %ItemCount% days/1 day} of trail rations
1, a Bottle of brandy
1, a Bottle of wine
1, a fancy peacock feather
1, a battered tankard
1, a [Miscellaneous.Metal] knife
1, a sealed flask containing 1 dose of poison ([~Equipment.Poisons])
2, a suif of [~Equipment.Armors Only] armor
1, a [~Equipment.Shields Only]
1, [~Equipment.MiscItems]
1, [~Equipment.Weapons]

:ToolsType
3,Thieves'
2,[Miscellaneous.Occupations]'s
1,Artisan's
1,Carpenter's
1,Surgeon's
1,Craftsman's

#;Musical Instruments
#6,[Musical Instruments Common][CalculatePrice=1]
#3,[Musical Instruments Uncommon][CalculatePrice=2]
#1,[Musical Instruments Rare][CalculatePrice=3]

;Musical Instruments
6,[Equipment.Musical Instruments Common][CalculatePrice=1]
3,[Equipment.Musical Instruments Uncommon][CalculatePrice=2]
1,[Equipment.Musical Instruments Rare][CalculatePrice=3]

:LockQuality
1-50,Poor Lock (Disable Device: DC 15)
51-75,Simple Lock (Disable Device: DC 20)
76-85,Average Lock (Disable Device: DC 25)
86-95,Good Lock (Disable Device DC: 30)
96-100,Superior Lock (Disable Device: DC 40)

:CalculatePrice
# Low Price
1,|Cost={Dice~1d10+50}|
# Moderate Price
2,|Cost={Dice~1d10+100}|
# High Price
3,|Cost={Dice~1d10+500}|