# ------------------------------------------------------
# Last Update: verified by Valminder September 07, 2007.
# ------------------------------------------------------
#
# Copyright 1994 by Wintertree Software
#
# Modified by Valminder, August 2007.
#    Adapted for TSv5.
#    I changed the "START" line.
#    Added link to:
#       Group Namer.BuildGroup
#       Miscellaneous.ColorNoType
#       Orc Tribes.Generate
# Modified by Valminder, September 2007.
#    Added MercNameMF subtable for the NAMES Generator.
#

%Gender%,1
%GenderType%,0
%Type%,0

@Gender,4,Please choose a Gender,Male,Female,Random,Both (for Common names only)
@Type,1,Choose Type of Name,Common,Formal Name,Mercenary Name

:Start
1,{Status~By Winterfree 1994, Valminder 2007}[Generate]

:Generate
1,[GenType=%Type%]

:GenType
1,[GenName=%Gender%]
2,[FormalName] (%GenderType%)
3,[MercName] (%GenderType%)

:GenName
1,[MaleName] (M)
2,[FemaleName] (F)
3,|Gender={Dice~1d2}|[GenName=%Gender%]
4,[MaleName] (M) / [FemaleName] (F)

;FormalName
3,[Adjective] [Name=%Gender%]
3,[Name=%Gender%] the [Adjective]
3,[Name=%Gender%] of [Town Names.Generate]
1,[Name=%Gender%]

:FormalNameMF
1,|Gender=1|[FormalName] (M)
2,|Gender=2|[FormalName] (F)

;MercName
4,[Name=%Gender%] the [Adjective]
5,[Name=%Gender%] of [Military]
1,[Name=%Gender%]

:MercNameMF
1,|Gender=1|[MercName] (M)
2,|Gender=2|[MercName] (F)

;Military
1,the Royal Army
2,the Militia
2,the Guards
3,the [~Orc Tribes.Generate]
4,[~Group Namer.BuildGroup]

# ---------------------------------------------------
:Adjective
1-3,[Appearance]
4-5,[Attribute]
6,[His_Color]

:Appearance
1,Ugly
2,Handsome
3,Plain
4,Young
5,Big
6,Little
7,Hairy
8,Bald
9,Fat
10,Tall

:Attribute
1,Hungry
2,Lazy
3,Greedy
4,Grouchy
5,Strong
6,Cold
7,Hot-Headed
8,Crazy
9,Noisy
10,Quiet

:His_Color
1,Red
2,Green
3,Blue
4,Black
5,Dark
6,White
7,{Cap~[Miscellaneous.ColorNoType]}

# ---------------------------------------------------
:Name
1,[MaleName]|GenderType=M|
2,[FemaleName]|GenderType=F|
3,{If~{Dice~1d10}<=9?[MaleName]|GenderType=M|/[FemaleName]|GenderType=F|}
4,[Name=3]

:MaleName
1-9,[M_First_Syllable][M_Last_Syllable]
10-12,[M_First_Syllable][M_Middle_Syllable][M_Last_Syllable]

:M_First_Syllable
1,Dar
2,Mar
3,Bel
4,Var
5,Nes
6,Lan
7,Tor
8,Yan
9,Rob
10,Del
11,Car
12,Cam
13,Wan
14,Shar
15,Nor
16,Bran
17,Von
18,Ras
19,Ber
20,Kel

:M_Middle_Syllable
1,ar
2,an
3,or
4,a
5,id
6,ot
7,en
8,os
9,i
10,e
11,is
12,at
13,od
14,er
15,e

:M_Last_Syllable
1,rik
2,ran
3,end
4,len
5,al
6,ar
7,or
8,dak
9,ol
10,ey
11,arn
12,and
13,art
14,dey
15,sey
16,ric
17,rek
18,ent
19,alt
20,fey

:FemaleName
1-4,[F_First_Syllable][F_Last_Syllable]
5-10,[F_First_Syllable][F_Middle_Syllable][F_Last_Syllable]
11-12,[F_First_Syllable][F_Middle_Syllable][F_Middle_Syllable][F_Last_Syllable]

:F_First_Syllable
1,Kar
2,Min
3,Kel
4,Var
5,Nas
6,Lan
7,Dor
8,Thal
9,Los
10,Mel

:F_Middle_Syllable
1,arth
2,as
3,or
4,al
5,ir
6,oth
7,en
8,os
9,ir
10,il

:F_Last_Syllable
1,kia
2,ti
3,ena
4,i
5,sa
6,da
7,tha
8,na
9,sti
10,ria
